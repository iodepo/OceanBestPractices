# Get IRI for a given term.

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
SELECT DISTINCT ?xs
#FROM <http://purl.obolibrary.org/obo/merged/ENVO>
WHERE {
  ?xs rdfs:label ?xl .
  FILTER (?xl = 'water'^^xsd:string)
}

#####
# Get has exact synonym and alternative label for an owl ontology.
#####

SPARQL 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT DISTINCT ?annotatedTarget ?annotatedPropertyLabel
#FROM <http://purl.obolibrary.org/obo/chebi.owl> 
WHERE {
  ?nodeID owl:annotatedSource ?xs .
  ?nodeID owl:annotatedProperty ?annotatedProperty .
  ?nodeID owl:annotatedTarget ?annotatedTarget .
  ?nodeID ?aaProperty ?aaPropertyTarget .
  OPTIONAL {?annotatedProperty rdfs:label ?annotatedPropertyLabel} .
  OPTIONAL {?aaProperty rdfs:label ?aaPropertyLabel} .
  FILTER ( isLiteral( ?annotatedTarget ) ) .
  FILTER ( ?aaProperty NOT IN ( owl:annotatedSource, rdf:type, owl:annotatedProperty, owl:annotatedTarget ) )
  {
    SELECT DISTINCT ?xs WHERE {
      ?xs rdfs:label ?xl .
      FILTER (?xl = 'water'^^xsd:string)
    }
  }
}

######
# Get the URI and labels for all Concepts in a SKOS vocabulary.
######

SPARQL
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT *
FROM <http://sea-data-net.com>
WHERE { ?s rdf:type skos:Concept . ?s skos:prefLabel ?o . } 

######
# Get the narrower, related, and broader terms for a given label in a SKOS vocabulary.
######

SPARQL
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?narrowerLabel ?broaderLabel ?relatedLabel
FROM <http://sea-data-net.com>
WHERE { 
  ?concept skos:prefLabel ?prefLabel .
  FILTER (str(?prefLabel) = "mass spectrometers")
  OPTIONAL { 
    ?concept skos:broader ?broader . 
    ?broader skos:prefLabel ?broaderLabel .
  }
  OPTIONAL { 
    ?concept skos:narrower ?narrower . 
    ?narrower skos:prefLabel ?narrowerLabel .
  }
  OPTIONAL { 
    ?concept skos:related ?related . 
    ?related skos:prefLabel ?relatedLabel .
  }
}

######
# Get the narrower, related, and broader terms for a given Concept URI in a SKOS vocabulary.
######

SPARQL
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?narrowerLabel ?broaderLabel ?relatedLabel
WHERE { 
  OPTIONAL { 
    <http://vocab.nerc.ac.uk/collection/L05/current/LAB16/> skos:broader ?broader . 
    ?broader skos:prefLabel ?broaderLabel .
  }
  OPTIONAL { 
    <http://vocab.nerc.ac.uk/collection/L05/current/LAB16/> skos:narrower ?narrower . 
    ?narrower skos:prefLabel ?narrowerLabel .
  }
  OPTIONAL { 
    <http://vocab.nerc.ac.uk/collection/L05/current/LAB16/> skos:related ?related . 
    ?related skos:prefLabel ?relatedLabel .
  }
}

######
# Get the sameAs property for a given label in a SKOS vocabulary.
######

SPARQL
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT ?sameAs
FROM <http://sea-data-net.com>
WHERE {
  ?concept skos:prefLabel ?prefLabel .
  FILTER (str(?prefLabel) = 'seismic refraction systems')
  ?concept owl:sameAs ?sameAs .
}

######
# Get synonyms and sameAs across all ontology formats.
######
SPARQL 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
SELECT DISTINCT ?annotatedTarget ?annotatedPropertyLabel ?sameAsConcept ?sameAsLabel
WHERE {
 {
  ?nodeID owl:annotatedSource ?xs .
  ?nodeID owl:annotatedProperty ?annotatedProperty .
  ?nodeID owl:annotatedTarget ?annotatedTarget .
  ?nodeID ?aaProperty ?aaPropertyTarget .
  OPTIONAL {?annotatedProperty rdfs:label ?annotatedPropertyLabel} .
  OPTIONAL {?aaProperty rdfs:label ?aaPropertyLabel} .
  FILTER ( isLiteral( ?annotatedTarget ) ) .
  FILTER ( ?aaProperty NOT IN ( owl:annotatedSource, rdf:type, owl:annotatedProperty, owl:annotatedTarget ) )
  {
    SELECT DISTINCT ?xs WHERE {
      ?xs rdfs:label ?xl .
      FILTER (?xl = 'water'^^xsd:string)
    }
  }
 }
 UNION 
 {
  SELECT ?sameAsConcept ?sameAsLabel
  WHERE {
    ?concept skos:prefLabel ?prefLabel .
    FILTER (str(?prefLabel) = 'NAVSTAR Global Positioning System receivers')
    ?concept owl:sameAs ?sameAsConcept .
    ?sameAsConcept skos:prefLabel ?sameAsLabel .
  }
 }
}


######
# Get all labels (across ontology formats) for every graph.
######

SPARQL
PREFIX dc:<http://purl.org/dc/elements/1.1/> 
PREFIX skos:<http://www.w3.org/2004/02/skos/core#> 
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
SELECT DISTINCT ?s ?label 
WHERE {
  {
    SELECT DISTINCT ?s ?label
    FROM <http://vocab.nerc.ac.uk/collection/L05/current/> 
    WHERE { 
       ?s a owl:Class . 
       ?s rdfs:label ?label . 
       FILTER regex(str(?s), "SeaData devices_") 
    } 
  } 
  UNION 
  {
    SELECT DISTINCT ?s ?label 
    FROM <http://vocab.nerc.ac.uk/collection/L05/current/> 
    WHERE { 
     ?s rdf:type skos:Concept . 
     ?s skos:prefLabel ?label . 
    }
  } 
} 
ORDER BY ?label 
LIMIT 200 
OFFSET 0

######
#
######

SPARQL 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT * 
#FROM <http://purl.obolibrary.org/obo/chebi.owl> 
WHERE {
  ?nodeID owl:annotatedSource <http://purl.obolibrary.org/obo/CHEBI_15377> .
  #?nodeID rdf:type owl:Annotation .
  ?nodeID owl:annotatedProperty ?annotatedProperty .
  ?nodeID owl:annotatedTarget ?annotatedTarget .
  ?nodeID ?aaProperty ?aaPropertyTarget .
  OPTIONAL {?annotatedProperty rdfs:label ?annotatedPropertyLabel} .
  OPTIONAL {?aaProperty rdfs:label ?aaPropertyLabel} .
  FILTER ( isLiteral( ?annotatedTarget ) ) .
  FILTER ( ?aaProperty NOT IN ( owl:annotatedSource, rdf:type, owl:annotatedProperty, owl:annotatedTarget ) )
}

######
# Get fuzzy matching (autocomplete) labels across all ontology types.
######

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
SELECT DISTINCT ?label ?annotatedTarget ?annotatedProperty  
WHERE { 
  { 
    SELECT DISTINCT ?label 
    WHERE { 
      ?s rdf:type owl:Class ; rdfs:label ?label . 
      FILTER (STRSTARTS(lcase(str(?label)), '" + params.input.toLowerCase() + "')) 
    }
  } 
  UNION
  {
    SELECT DISTINCT ?label 
    WHERE { 
      ?s rdf:type skos:Concept . 
      ?s skos:prefLabel ?label .
      FILTER ( STRSTARTS(lcase(str(?label)), '" + params.input.toLowerCase() + "'))
    }
  }
  UNION 
  { 
    SELECT DISTINCT ?annotatedTarget ?annotatedPropertyLabel 
    WHERE { 
      ?nodeID owl:annotatedSource ?xs . 
      ?nodeID owl:annotatedProperty ?annotatedProperty . 
      ?nodeID owl:annotatedTarget ?annotatedTarget . 
      ?annotatedProperty rdfs:label ?annotatedPropertyLabel . 
      FILTER ( isLiteral( ?annotatedTarget ) ) . 
      FILTER ( STR(?annotatedPropertyLabel) = 'has_exact_synonym' || STR(?annotatedPropertyLabel) = 'alternative_label') 
      { 
        SELECT DISTINCT ?xs 
        WHERE { 
          ?xs rdfs:label ?xl . 
          FILTER (?xl = '" + params.input + "'^^xsd:string) 
        } 
      }
    } 
  }
}
LIMIT 10 

SPARQL
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
SELECT DISTINCT ?o
WHERE 
{ 
?s rdf:type owl:Class .
?s ?p ?o . 
FILTER (STRSTARTS(str(?o), 'ocea'))
}
LIMIT 10 

SPARQL
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?label 
WHERE 
{ 
?s rdf:type owl:Class ;
rdfs:label ?label .
FILTER (STRSTARTS(str(?label), 'ocea'))
}
LIMIT 20

# Get subclasses of term http://purl.obolibrary.org/obo/ENVO_00000002

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
SELECT *
FROM <http://purl.obolibrary.org/obo/merged/ENVO>
WHERE {
 ?s rdf:type owl:Class .
 ?s rdfs:subClassOf <http://purl.obolibrary.org/obo/ENVO_00000002> .
}

# Get parents of term water

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
SELECT ?x ?parent ?label
FROM <http://purl.obolibrary.org/obo/merged/ENVO>
WHERE {
 ?x rdf:type owl:Class .
 ?x rdfs:subClassOf ?parent .
 OPTIONAL { ?parent rdfs:label ?label }
 FILTER regex(str(?x), "http://purl.obolibrary.org/obo/CHEBI_35231")
}

# Get parents of term water with relation and reference labels.

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
SELECT ?term ?superTerm ?superLabel ?relLabel ?ref ?refLabel
FROM <http://purl.obolibrary.org/obo/merged/ENVO>
WHERE {
 ?term rdfs:subClassOf ?superTerm .
 OPTIONAL { ?superTerm rdfs:label ?superLabel }
 OPTIONAL { ?superTerm owl:onProperty ?rel . ?rel rdfs:label ?relLabel . }
 OPTIONAL { ?superTerm owl:someValuesFrom ?ref . ?ref rdfs:label ?refLabel . }
 FILTER (?term = <http://purl.obolibrary.org/obo/ENVO_00002006>)
}

# Get all graphs

SELECT  DISTINCT ?g 
   WHERE  { GRAPH ?g {?s ?p ?o} } 
ORDER BY  ?g

# Get children of water

prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
prefix owl: <http://www.w3.org/2002/07/owl#> 
SELECT ?x ?label
FROM <http://purl.obolibrary.org/obo/merged/ENVO>
WHERE {
 ?x rdfs:subClassOf <http://purl.obolibrary.org/obo/ENVO_00002006>
 OPTIONAL { ?x rdfs:label ?label }
}

# Get uses of water in this ontology

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT DISTINCT ?ref ?refp ?label ?o ?rl FROM <http://purl.obolibrary.org/obo/merged/ENVO> WHERE {
  ?ref ?refp ?o .
  FILTER ( ?refp IN ( owl:equivalentClass, rdfs:subClassOf ) ) .
  OPTIONAL { ?ref rdfs:label ?label } .
  OPTIONAL { ?o owl:onProperty ?rel . ?rel rdfs:label ?rl } .
  {
    {
      SELECT ?s ?p ?o FROM <http://purl.obolibrary.org/obo/merged/ENVO> WHERE {
        ?o ?p ?s .

        FILTER ( ?p IN ( rdf:first, rdf:rest, owl:intersectionOf, owl:unionOf, owl:someValuesFrom, owl:hasValue, owl:allValuesFrom, owl:complementOf, owl:inverseOf, owl:onClass, owl:onProperty ) )
      }
    }

    FILTER ( ?s = <http://purl.obolibrary.org/obo/ENVO_00002006> )
  }
}
ORDER BY ?label
